//noinspection GradleDependency
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {

    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'com.example.myapp.MyEventBusIndex']
            }
        }

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')

    // AndroidSupport
    api 'com.android.support:design:28.0.0'
    api 'com.android.support:appcompat-v7:28.0.0'

    // Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // AndroidUtilCode
    // https://github.com/Blankj/AndroidUtilCode
    api 'com.blankj:utilcode:1.23.7'

    // Bugly
    // https://bugly.qq.com/docs/user-guide/instruction-manual-android/?v=20180709165613
    api 'com.tencent.bugly:crashreport:3.0.0'
    api 'com.tencent.bugly:nativecrashreport:3.7.0'

    // EventBus
    // https://github.com/greenrobot/EventBus
    api 'org.greenrobot:eventbus:3.1.1'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.1.1'

    // DBFlow
    // https://github.com/agrosner/DBFlow
    api 'com.github.Raizlabs.DBFlow:dbflow:4.2.4'
    api 'com.github.Raizlabs.DBFlow:dbflow-core:4.2.4'
    api 'com.github.Raizlabs.DBFlow:dbflow-sqlcipher:4.2.4'
    annotationProcessor 'com.github.Raizlabs.DBFlow:dbflow-processor:4.2.4'

    // BGASwipeBackLayout
    // https://github.com/bingoogolapple/BGASwipeBackLayout-Android
    api 'cn.bingoogolapple:bga-swipebacklayout:1.2.0'
}

//-------------------- jitPack生成文档 --------------------
tasks.withType(JavaCompile) { //指定编码
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar) { //打包源码
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) { //制作文档(Javadoc)
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
